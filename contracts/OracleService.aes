// ISC License
//
// Copyright (c) 2020, aeternity developers
//
// Permission to use, copy, modify, and/or distribute this software for any
// purpose with or without fee is hereby granted, provided that the above
// copyright notice and this permission notice appear in all copies.
//
// THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES WITH
// REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF MERCHANTABILITY
// AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY SPECIAL, DIRECT,
// INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES WHATSOEVER RESULTING FROM
// LOSS OF USE, DATA OR PROFITS, WHETHER IN AN ACTION OF CONTRACT, NEGLIGENCE
// OR OTHER TORTIOUS ACTION, ARISING OUT OF OR IN CONNECTION WITH THE USE OR
// PERFORMANCE OF THIS SOFTWARE.


// THIS IS NOT SECURITY AUDITED
// DO NEVER USE THIS WITHOUT SECURITY AUDIT FIRST

@compiler >= 4.2

include "List.aes"

contract OracleService =

  type oracle_type = oracle(string, string)
  type oracle_query_tuples = list(oracle_type * oracle_query(string, string))

  record success_claim =
    { success : bool
    , account : address
    , percentage : int }

  record state =
    { trusted_oracles : map(int, oracle_type)
    , oracle_queries : map(string, oracle_query_tuples)
    , minimum_amount_of_oracles : int
    , success_claimed_urls : map(string, success_claim)
    , owner : address }

  entrypoint init(minimum_amount_of_oracles : int, owner : address) =
    { trusted_oracles = {},
      oracle_queries = {},
      minimum_amount_of_oracles = minimum_amount_of_oracles,
      success_claimed_urls = {},
      owner = owner }

  // TODO: fix Map.size not working as id if allow deletion
  stateful entrypoint add_oracle(oracle : oracle_type) =
    require_owner()
    require(Oracle.check(oracle), "ORACLE_CHECK_FAILED")
    put(state{ trusted_oracles[Map.size(state.trusted_oracles)] = oracle })

  stateful entrypoint remove_oracle(id : int) =
    require_owner()
    put(state{ trusted_oracles = Map.delete(id, state.trusted_oracles) })

  stateful entrypoint set_minimum_amount_of_oracles(minimum_amount_of_oracles : int) =
    require_owner()
    put(state{ minimum_amount_of_oracles = minimum_amount_of_oracles })

  payable stateful entrypoint query_oracle(url : string, expected_account : address) =
    let query_fee = estimate_query_fee()
    require(query_fee == Call.value, String.concat("QUERY_FEE_NOT_MATCHING_", Int.to_str(query_fee)))
    let query_string = String.concat(Address.to_str(expected_account), String.concat(";", url))
    let query_oracle = (oracle) => (oracle, Oracle.query(oracle, query_string, Oracle.query_fee(oracle), RelativeTTL(20), RelativeTTL(20)))
    let queries = List.map(query_oracle, checked_oracles())
    put(state{ oracle_queries = state.oracle_queries{[url] = queries }})

  stateful entrypoint check_persist_claim(url : string, account : address, recheck : bool) : success_claim =
    let success_claim = state.success_claimed_urls[url = { success = false, account = account, percentage = 0 }]
    if(success_claim.success && !recheck)
      success_claim
    else
      let checked_claim = check_claim(url, account)
      put(state{ success_claimed_urls[url] = checked_claim })
      checked_claim

  // GETTER FUNCTIONS

  entrypoint get_state() = state

  entrypoint estimate_query_fee() : int =
    estimate_query_fee_iterate(0, checked_oracles())

  // INTERNAL FUNCTIONS

  stateful function check_claim(url : string, account : address) : success_claim =
    let oracle_answers = check_oracle_answers(url, account)
    let answers_length = List.length(oracle_answers)
    require(answers_length >= state.minimum_amount_of_oracles, "MORE_ORACLE_ANSWERS_REQUIRED")
    let is_positive = (answer) => switch(answer)
      Some(response) => response
      None => false
    let positive_answers_length = List.length(List.filter(is_positive, oracle_answers))
    let percentage = positive_answers_length * 100 / answers_length
    let success = percentage > 50
    if(success) put(state{ oracle_queries = Map.delete(url, state.oracle_queries) }) else ()
    { success = success, account = account, percentage = percentage }

  function check_oracle_answers(url : string, account : address) : list(option(bool)) =
    let check_query = (oracle, query) => switch(Oracle.get_answer(oracle, query))
      Some(response) => Some(response == Address.to_str(account))
      None => None
    [ check_query(oracle, query) | (oracle, query) <- state.oracle_queries[url], if (check_oracle(oracle)) ]

  function
    estimate_query_fee_iterate : (int, list(oracle_type)) => int
    estimate_query_fee_iterate(fee_acc, [])      = fee_acc
    estimate_query_fee_iterate(fee_acc, o :: os) = estimate_query_fee_iterate(fee_acc + Oracle.query_fee(o), os)

  function check_oracle(o : oracle_type) : bool = Oracle.check(o)

  function checked_oracles() : list(oracle_type) =
    let oracles = [ oracle | (_, oracle) <- Map.to_list(state.trusted_oracles), if (check_oracle(oracle)) ]
    require(List.length(oracles) >= state.minimum_amount_of_oracles, "MORE_ORACLES_REQUIRED")
    oracles

  function require_owner() =
    require(Call.caller == state.owner, "OWNER_REQUIRED")
